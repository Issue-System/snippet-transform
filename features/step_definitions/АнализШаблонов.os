// Реализация шагов BDD-фич/сценариев c помощью фреймворка https://github.com/artbear/1bdd

Перем БДД; //контекст фреймворка 1bdd

// Метод выдает список шагов, реализованных в данном файле-шагов
Функция ПолучитьСписокШагов(КонтекстФреймворкаBDD) Экспорт
	БДД = КонтекстФреймворкаBDD;

	ВсеШаги = Новый Массив;

	ВсеШаги.Добавить("ФайлВРабочемКаталогеСодержитСтроки");
	ВсеШаги.Добавить("ФайлВРабочемКаталогеНеСодержитСтроки");

	Возврат ВсеШаги;
КонецФункции

// Реализация шагов

// Процедура выполняется перед запуском каждого сценария
Процедура ПередЗапускомСценария(Знач Узел) Экспорт
	
КонецПроцедуры

// Процедура выполняется после завершения каждого сценария
Процедура ПослеЗапускаСценария(Знач Узел) Экспорт
	
КонецПроцедуры

//Файл "result/vscode1.code-snippets" в рабочем каталоге содержит строки
//| "scope": "bsl" |
//| "prefix": "Общий модуль" |
//| "prefix": "DEPRECATED" |
//| "prefix": "Инкремент" |
Процедура ФайлВРабочемКаталогеСодержитСтроки(Знач ФайлРабочегоКаталога, Знач ТаблицаСтрок) Экспорт
	
	ФайлРабочегоКаталога = БДД.ПолучитьПутьФайлаСУчетомПеременныхКонтекста(ФайлРабочегоКаталога);
	
	Чтение = Новый ЧтениеТекста(ОбъединитьПути(БДД.ПолучитьИзКонтекста("РабочийКаталог"), ФайлРабочегоКаталога), КодировкаТекста.UTF8); 	
	СтрокаГдеИщем = Чтение.Прочитать();
	Чтение.Закрыть();
	
	СтрокаРасхождений = "";
	Для Каждого СтрТаблицы Из ТаблицаСтрок Цикл
		
		СтрокаЧтоИщем = СтрТаблицы[0];
		Если Найти(СтрокаГдеИщем, СтрокаЧтоИщем) = 0 Тогда
			СтрокаРасхождений = СтрШаблон(
			"%1
			|	Не найдена подстрока <%2>", СтрокаРасхождений, СтрокаЧтоИщем);
		КонецЕсли;

	КонецЦикла;
	Если Не ПустаяСтрока(СтрокаРасхождений) Тогда
		СтрокаРасхождений = СтрШаблон(
		"Не нашли одну из подстрок таблицы:
		|%2
		|в строке:
		|%1", СтрокаГдеИщем, СтрокаРасхождений);
		Ожидаем.Что(Истина, СтрокаРасхождений).ЭтоЛожь();
	КонецЕсли;

КонецПроцедуры

//Файл "result/vscode1.code-snippets" в рабочем каталоге не содержит строки
//| "scope": "bsl" |
//| "prefix": "Общий модуль" |
//| "prefix": "DEPRECATED" |
//| "prefix": "Инкремент" |
Процедура ФайлВРабочемКаталогеНеСодержитСтроки(Знач ФайлРабочегоКаталога, Знач ТаблицаСтрок) Экспорт
	
	ФайлРабочегоКаталога = БДД.ПолучитьПутьФайлаСУчетомПеременныхКонтекста(ФайлРабочегоКаталога);
	
	Чтение = Новый ЧтениеТекста(ОбъединитьПути(БДД.ПолучитьИзКонтекста("РабочийКаталог"), ФайлРабочегоКаталога), КодировкаТекста.UTF8); 	
	СтрокаГдеИщем = Чтение.Прочитать();
	Чтение.Закрыть();
	
	СтрокаРасхождений = "";
	Для Каждого СтрТаблицы Из ТаблицаСтрок Цикл
		
		СтрокаЧтоИщем = СтрТаблицы[0];
		Если Найти(СтрокаГдеИщем, СтрокаЧтоИщем) <> 0 Тогда
			СтрокаРасхождений = СтрШаблон(
			"%1
			|	Найдена подстрока <%2>", СтрокаРасхождений, СтрокаЧтоИщем);
		КонецЕсли;

	КонецЦикла;
	Если Не ПустаяСтрока(СтрокаРасхождений) Тогда
		СтрокаРасхождений = СтрШаблон(
		"Нашли одну из подстрок таблицы:
		|%2
		|в строке:
		|%1", СтрокаГдеИщем, СтрокаРасхождений);
		Ожидаем.Что(Истина, СтрокаРасхождений).ЭтоЛожь();
	КонецЕсли;

КонецПроцедуры
